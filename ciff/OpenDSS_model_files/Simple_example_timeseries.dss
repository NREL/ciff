clear

//DEFINE CIRCUIT AND PATH
set DefaultBaseFrequency=50
set datapath=C:\Users\epohl\Desktop\CIFF_Dehli_Presentation\Git_repo\ciff\OpenDSS_model_files\inputs_outputs_timeseries
new circuit.simple_example_timeseries basekv=132 pu=1.0 angle=0 frequency=50 phases=3

//DEFINE SUBSTATION TRANSFORMER
new transformer.substation phases=3 windings=2 buses=(sourcebus, A) conns=(wye, wye)
~ kvs=(132, 33) kvas=(30000, 30000) %loadloss=0.1 xhl=12.5

//DEFINE LINECODES FOR PRIMARY AND SECONDARY
new linecode.795_AAC nphases=3 R1=0.0746 X1=0.373 R0=0.1796 X0=0.9451 units=km normamps=810 emergamps=972
new linecode.336_ACSR nphases=3 R1=0.1731 X1=0.4028 R0=0.367 X0=1.10194 units=km normamps=475 emergamps=570
new linecode.4_0_ACSR nphases=3 R1=0.2735 X1=0.4183 R0=0.5437 X0=1.079 units=km normamps=325 emergamps=390
new linecode.1_0_ACSR nphases=3 R1=0.696 X1=0.6872 R0=0.696 X0=0.682 units=km normamps=220 emergamps=264
new linecode.2_ACSR nphases=3 R1=1.0412 X1=0.7627 R0=1.0412 X0=0.7627 units=km normamps=165 emergamps=198
new linecode.500_AL nphases=3 R1=0.115 X1=0.24 R0=0.2927 X0=2.143 units=km normamps=445 emergamps=534
new linecode.1000_AL nphases=3 R1=0.5915 X1=0.2351 R0=0.2368 X0=2.073 units=km normamps=575 emergamps=690

//ADD PRIMARY LINES
new line.lineA-B bus1=A bus2=B length=5 phases=3 units=km linecode=1_0_ACSR
new line.lineB-C bus1=B bus2=C length=3 phases=3 units=km linecode=2_ACSR

//DISTRIBUTION TRANSFORMER
new transformer.dist1 phases=3 windings=2 XHL=2
~ wdg=1 bus=C conn=wye kV=33 3 kva= 1000 %r=0.55 XHT=1
~ wdg=2 bus=D conn=wye kV=0.4 kva= 1000 %r=0.55 XLT=1 

//ADD SECONDARY LINES
new line.lineD-E bus1=D bus2=E length=0.025 phases=3 units=km linecode=1000_AL

//LOADS
new load.primary_load1 bus1=B phases=3 kv=33 kw=6750 kvar=3269 model=1 !7500kVA at pf=0.90
new load.secondary_load1 bus1=E phases=3 kv=0.4 kw=315 kvar=153 model=1 !350kVA at pf=0.90

//VOLTAGE BASES
set voltagebases=[132 33 0.4]
calcvoltagebases 

//MONITORS
!Substation
new monitor.substationpowers element=transformer.substation terminal=1 mode=1 ppolar=no
new monitor.substationVI element=transformer.substation terminal=2 mode=0 

!primary_load
new monitor.primary_load1 element=load.primary_load1 terminal=1 mode=1 ppolar=no

//ENERGYMETERS
new energymeter.Grid element=transformer.substation terminal=1

//ADD BUS COORDINATES
Buscoords Buscoords.csv

//SUBSTATION LTC
!new regcontrol.substation transformer=substation winding=2 tapwinding=1 vreg=(123) ptratio=(33000 3 sqrt / 120 /) band=2
!BatchEdit transformer.substation.* maxtap=1.05 mintap=0.85

//CAPACITOR BANK
!new capacitor.cap bus1=B phases=3 Kvar=1200 kV=33 Conn=wye basefreq=50

//CONTROL MODES
set controlmode=static
!set mode=snapshot
set mode=daily

//DEFINE LOADSHAPES
new loadshape.daily npts=24 interval=1.0 csvfile=demand_daily.txt
!plot loadshape object=daily
BatchEdit Load..* daily=daily status=variable

//SOLVE POWERFLOW
solve

//RESULTS

//Export to CSV
/*
export monitors substationVI
export monitors substationpowers
export currents
export meters
export losses
*/

//Visualize bus diagrams
/*
visualize what={voltages} element=transformer.substation
visualize What={voltages} element=line.lineA-B
visualize What={currents} element=line.lineA-B
*/

//Show text files
/*
show voltages
show currents
show deltaV
show losses
show powers
*/

//Plot Voltage Profile
!plot profile phases=all
/*
plot monitor object=substationpowers channels=(1 2 3 4 5 6)
plot monitor object=substationVI channels=(1 3  5)
*/

//Plot Circuit Diagram
/*
Set marktransformers=yes
Set TransMarkerSize=3
plot Circuit
*/






